#!/bin/zsh
#!/usr/bin/env zsh

[ ! -x "$FASTBOOT_BIN" ] && FASTBOOT_BIN=$(whence -p fastboot)
[ ! -x "$ADB_BIN" ] && ADB_BIN=$(whence -p adb)

declare -a ANDROID_DEVICES_COLUMNS=(serial devrev build protocol transport)
declare -A ANDROID_DEVICES_HEADERS=(
  serial    " Serial No"
  devrev    " Device (Revision)"
  build     "  Android Build"
  protocol  "  Mode "
  transport " Transport"
)

function get_android_adb_var()
{
  local aserial=$1
  local varname=$2
  echo -n $($ADB_BIN -s $aserial shell getprop $varname)
}

function get_android_fb_var()
{
  local aserial=$1
  local varname=$2
  local pre=
  [ -z "$(command -v timeout)" ] || pre="timeout 1s"
  local val=$(${=pre} $FASTBOOT_BIN -s $aserial getvar $varname 2>&1 | grep ^${varname} | sed -E "s/${varname}: //g")
  [ -n "$val" -a "$val" != "unknown variable" ] && [[ "$val" =~ ^[[:alnum:]].* ]] && echo $val
}

function get_android_adb_info()
{
  local aserial=${1:-$ANDROID_SERIAL}
  [ "$aserial" = "" ] && return 1

  if [ $# -gt 1 ]; then
    local state=${2[(w)1]}
    local transport=${2[(w)2]}
  else
    local state=$($ADB_BIN -s $aserial get-state 2>/dev/null)
    local transport=$($ADB_BIN -s $aserial get-devpath 2>/dev/null)
  fi

  declare -A devinfo=(
    serial      "$aserial"
    protocol    "adb"
    transport   $transport
  )

  if [ "$state" != "device" ]; then
    devinfo[dev]="Unknown"
    devinfo[protocol]+=" ($state)"
  else
    devinfo[dev]="$(get_android_adb_var $aserial ro.hardware)"
    devinfo[rev]="$(get_android_adb_var $aserial ro.revision)"
    devinfo[build]="$(get_android_adb_var $aserial ro.build.id)"
  fi
  declare -p devinfo
}

function get_android_fb_info()
{
  local aserial=${1:-$ANDROID_SERIAL}
  [ "$aserial" = "" ] && return 1

  if [ $# -lt 2 ]; then
    local fb_connected=$($FASTBOOT_BIN devices -l | grep ^$aserial)
    local transport=${fb_connected[(w)3]}

    [ -n "$fb_connected" ] || return 1
  else
    local transport=${2[(w)2]}
  fi

  declare -A devinfo=(
    serial      "$aserial"
    protocol    "fastboot"
    transport   $transport
  )

  local dev=$(get_android_fb_var $aserial product)
  # if no output found now, it's likely going to fail for next ones too
  if [ -n "$dev" ]; then
    devinfo[dev]=$dev
    devinfo[rev]=$(get_android_fb_var $aserial hw-revision)
    devinfo[build]=$(get_android_fb_var $aserial version-bootloader)
  else
    devinfo[dev]="Unknown"
  fi
  declare -p devinfo
}

function print_row()
{
  [ $# -gt 0 ] || return

  eval "declare -A devinfo=${1#*=}"
  if [ -z "${devinfo[devrev]}" ]; then
    local tmp=${devinfo[dev]}
    [ -n "${devinfo[rev]}" -a "${devinfo[rev]}" != "0" ] && tmp+=" (${devinfo[rev]})"
    devinfo[devrev]=$tmp
  fi

  declare -a fields=()
  for k in "${(@k)ANDROID_DEVICES_COLUMNS}"; do
    fields+=${devinfo[$k]:-"N/A"}
  done
  echo ${(j:\t| :)fields}
}

function print_header()
{
  declare -A sep
  for k in "${(@k)ANDROID_DEVICES_COLUMNS}"; do
    local len=$(((${#ANDROID_DEVICES_HEADERS[$k]}+1)/2))
    sep[$k]=$(printf " -"%.0s {1..$len})
  done
  print_row "$(declare -p ANDROID_DEVICES_HEADERS)"
  print_row "$(declare -p sep)"
}

function list_android_devices_helper()
{
  while getopts 'h' arg; do
    case $arg in
      h) print_header ;;
    esac
  done
  shift $((OPTIND-1))

  if command adb start-server; then
    local adb_devices=$(command $ADB_BIN devices -l | sed -n "2,100p")
    for dev in ${(f)adb_devices}; do
      local aserial=${dev[(w)1]}
      local details=$(echo ${dev#* } | xargs)
      print_row "$(get_android_adb_info $aserial $details)"
    done
  fi

  local fb_devices=$(command $FASTBOOT_BIN devices -l)
  for dev in ${(f)fb_devices}; do
    local aserial=${dev%% *}
    local details=$(echo ${dev#* } | xargs)
    print_row "$(get_android_fb_info $aserial $details)"
  done
}

function list_android_devices()
{
  list_android_devices_helper $@ | column -t -s $'\t'
}

function show_android_device()
{
  local aserial=${1:-$ANDROID_SERIAL}
  local info=$(get_android_fb_info $ANDROID_SERIAL || get_android_adb_info $ANDROID_SERIAL)

  [ -n "$info" ] || return 1
  eval "declare -A devinfo=${info#*=}"

  >&2 echo -n "${devinfo[dev]}"
  [ -n "${devinfo[rev]}" -a "${devinfo[rev]}" != "0" ] && echo -n " (${devinfo[rev]})"
  [ -n "${devinfo[build]}" ] && >&2 echo -n ":${devinfo[build]# }"
  >&2 echo -n ":"
  echo -n "${aserial}"
  >&2 echo
}

function select_android_device_fzf()
{
  local devices=("${(@f)$(list_android_devices)}")
  if [ -z "$devices" ]; then
    echo "No devices connected!"
    return 1
  fi

  local fzf_header="Choose from available connected devices: "
  local fzf_args=(
    --height=$((3+${#devices[@]}))
    --exit-0
    --reverse
    --inline-info
  )
  if [ -n "$1" ]; then
    fzf_args+=(
      --select-1
      --query="$*"
    )
  fi

  local selection=$(echo "${(j:\n:)devices}" | fzf ${fzf_args} --header="${fzf_header}")
  # assumes serial is the first argument
  local aserial=${selection%% *}

  if [ -z "$aserial" ]; then
    if [ -n "$1" ]; then
      >&2 echo "No devices matching: \"$@\""
    else
      >&2 echo "No device selected"
    fi
    return 1
  fi

  export ANDROID_SERIAL=${aserial}
  >&2 echo -n "Selected device: "
  show_android_device
}

function select_android_device_list()
{
  local devices=("${(@f)$(list_android_devices)}")
  if [ -z "$devices" ]; then
    >&2 echo "No devices connected!"
    return 1
  fi

  local counter=0
  local selection=$1
  declare -a serial_list
  if [ -z "$selection" ]; then
    >&2 echo "Choose from connected devices"

    for dev in ${devices}; do
      let counter=counter+1
      serial_list[$counter]=${dev%% *}

      >&2 echo "${counter}. ${dev}"
    done

    read selection\?"Select option: "
  fi
  echo
  if ! [[ "$selection" =~ ^[0-9]+$ ]]; then
    >&2 echo "Invalid option \"$selection\". Should be numeric"
    return 1
  elif [ "${serial_list[$selection]}" = "" ]; then
    >&2 echo "Invalid option \"$selection\""
    return 1
  fi

  export ANDROID_SERIAL=${serial_list[$selection]}
  >&2 echo -n "Selected device #${selection}: "
  show_android_device
}

function usage()
{
  print "Usage: select_android_device [OPTIONS] [DEVICE]

If provided will try to find a device that matches closely DEVICE argument

Options:
  -a      Use list and prompt for device selection instead of fzf
  -f      Force use of fzf for device selection
  -l      List Android devices connected and exit
  -h      Print this help"
}

pref="auto"
while getopts 'aflhp?' arg; do
  case $arg in
    a) pref="list" ;;
    f) pref="fzf" ;;
    l) list_android_devices -h; return 0 ;;
    p) show_android_device; return 0 ;;
    h|?) usage; return 0; ;;
  esac
done
shift $((OPTIND-1))

if [ "$pref" = "auto" ]; then
  [ -n "$(command -v fzf)" ] && pref="fzf" || pref="list"
fi

case $pref in
  fzf)
    select_android_device_fzf $@ ;;
  list)
    select_android_device_list $@ ;;
  *)
    echo "Shouldn't be here ($pref)"
esac

